@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(PkgConfig MODULE)
pkg_check_modules(LAPACKE REQUIRED IMPORTED_TARGET lapacke)

# Try to find CBLAS - systems name it differently (cblas, blas, or openblas)
pkg_check_modules(CBLAS IMPORTED_TARGET cblas)
if(NOT CBLAS_FOUND)
  pkg_check_modules(CBLAS IMPORTED_TARGET blas)
endif()
if(NOT CBLAS_FOUND)
  pkg_check_modules(CBLAS REQUIRED IMPORTED_TARGET openblas)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/srukfTargets.cmake")

# Create srukf::srukf alias to the primary target (prefer shared if available)
if(NOT TARGET srukf::srukf)
  if(TARGET srukf::srukf_shared)
    add_library(srukf::srukf ALIAS srukf::srukf_shared)
  elseif(TARGET srukf::srukf_static)
    add_library(srukf::srukf ALIAS srukf::srukf_static)
  endif()
endif()

check_required_components(srukf)
